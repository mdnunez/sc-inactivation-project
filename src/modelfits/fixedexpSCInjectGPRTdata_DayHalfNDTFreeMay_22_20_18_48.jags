
model {

    #Between-condition variability in Rightward/Leftward drift rate
    deltarightsdcond ~ dgamma(1,1)

    #Between-condition variability in non-decision time
    tersdcond ~ dgamma(.3,1)

    #Between-session variability in non-decision time
    tersd ~ dgamma(.3,1)

    #Between-session variability in Speed-accuracy trade-off
    alphasd ~ dgamma(1,1)

    #Between-session variability in Rightward start point bias
    betasd ~ dgamma(.3,1)

    #Between-session variability in lapse trial probability
    problapsesd ~ dgamma(.3,1)

    #Between-session variability in Rightward/Leftward drift rate
    deltarightsd ~ dgamma(1,1)

    for (m in 1:nmonkeys) {
        ##########
        #Hierarchical DDM parameters
        ##########

        #Hierarchical Speed-accuracy trade-off
        alphahier[m] ~ dnorm(1, pow(.5,-2))

        #Hierarchical Rightward start point bias
        betahier[m] ~ dnorm(.5, pow(.25,-2))

        #Hierarchical lapse trial probability
        problapsehier[m] ~ dnorm(.3, pow(.15,-2))

        #Hierarchical Rightward/Leftward drift rate
        deltarighthier[m] ~ dnorm(0, pow(2, -2))

        for (e in 1:ninject) {

            #Hierarchical Non-decision time
            terhier[e,m] ~ dnorm(.5, pow(.25,-2))
        }
    }

    ##########
    #DDM parameters
    ##########

    for (d in 1:ndays) {

        #Speed-accuracy trade-off
        alpha[d] ~ dnorm(alphahier[monkeyday[d]], pow(alphasd,-2))T(0, 3)

        #Rightward start point bias
        beta[d] ~ dnorm(betahier[monkeyday[d]], pow(betasd,-2))T(0, 1)

        #Probability of a lapse trial
        problapse[d] ~ dnorm(problapsehier[monkeyday[d]], pow(problapsesd,-2))T(0, 1)
        probDDM[d] <- 1 - problapse[d]

        #Rightward/Leftward drift rate
        deltarightses[d] ~ dnorm(deltarighthier[monkeyday[d]], pow(deltarightsd, -2))

        for (c in 1:nconditions) {

            #Rightward/Leftward drift rate
            deltaright[d,c] ~ dnorm(deltarightses[d], pow(deltarightsdcond, -2))T(-9, 9)

        }

    }


    for (s in 1:nsessions) {

        #Non-decision time
        terses[s] ~ dnorm(terhier[inject[s],monkey[s]], pow(tersd,-2))

        for (c in 1:nconditions) {

            #Condition-level non-decision time
            ter[s,c] ~ dnorm(terses[s], pow(tersdcond, -2))T(0, 1)

        }

    }

    ##########
    # Wiener likelihoods
    for (i in 1:N) {

        # Log density for DDM process of rightward/leftward RT
        ld_comp2[i, 1] <- dlogwiener(rightwardRT[i], alpha[day[i]], ter[session[i],condition[i]], beta[day[i]], deltaright[day[i],condition[i]])

        # Log density for lapse trials (negative max RT to positive max RT)
        ld_comp2[i, 2] <- logdensity.unif(rightwardRT[i], -3, 3)

        # Select one of these two densities (Mixture of nonlapse and lapse trials)
        density2[i] <- exp(ld_comp2[i, DDMorLapse[i]] - Constant)
        
        # Generate a likelihood for the MCMC sampler using a trick to maximize density value
        Ones2[i] ~ dbern(density2[i])

        # Probability of mind wandering trials (lapse trials)
        DDMorLapse[i] ~ dcat( c(probDDM[day[i]], problapse[day[i]]) )
    }
}
